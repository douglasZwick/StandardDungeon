class Health : ZilchComponent
{
  sends DamageRequest : HealthEvent;
  sends WasDamaged : HealthEvent;
  sends HealRequest : HealthEvent;
  sends WasHealed : HealthEvent;
  sends Death : HealthEvent;
  
  
  [Property]
  var MaxHealth : Integer = 1;
  
  var CurrentHealth : Integer;
  
  
  function Initialize(init : CogInitializer)
  {
    this.CurrentHealth = this.MaxHealth;
    
    Zero.Connect(this.Owner, Events.DamageRequest, this.OnDamageRequest);
    Zero.Connect(this.Owner, Events.HealRequest, this.OnHealRequest);
  }
  
  
  function OnDamageRequest(event : HealthEvent)
  {
    this.CurrentHealth -= event.Damage;
    
    this.Owner.DispatchEvent(Events.WasDamaged, event);
    
    if (this.CurrentHealth <= 0)
    {
      this.Owner.DispatchEvent(Events.Death, event);
      
      if (!event.Handled)
        this.Owner.Destroy();
    }
  }
  
  
  function OnHealRequest(event : HealthEvent)
  {
    this.CurrentHealth += event.Healing;
    
    this.Owner.DispatchEvent(Events.WasHealed, event);
    
    if (this.CurrentHealth > this.MaxHealth)
      this.CurrentHealth = this.MaxHealth;
    
    // TODO: put in some feedback for hitting max HP in a heal
  }
}

class HealthEvent : ZilchEvent
{
  var Damage : Integer;
  var Healing : Integer;
  var Handled : Boolean = false;
}
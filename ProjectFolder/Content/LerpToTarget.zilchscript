class LerpToTarget : ZilchComponent
{
  [Property]
  var MoverPath = CogPath(".");  // the object that will move
  [Property]
  var FocusPath = CogPath();  // the object the mover will move to
  [Property][Slider(0, 1, 0.01)]
  var Snappiness : Real = 0.1;
  [Property]
  var Quantize : Boolean = false; // experimental and shitty
  [Property]
  var QuantumSubdivisions : Integer = 4;
  
  var Epsilon : Real = 1.0e-6;
  
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }
  
  
  function OnLogicUpdate(event : UpdateEvent)
  {
    var moverPosition = this.MoverPath.Transform.WorldTranslation;
    var focusPosition = this.FocusPath.Transform.WorldTranslation;
    var difference = focusPosition - moverPosition;
    
    // TODO: this could maybe be slightly rewritten
    // to take snappiness into account
    if (Math.LengthSq(difference) >= this.Epsilon)
    {
      var movement = difference * this.Snappiness;
      var calculatedPosition = moverPosition + movement;
      
      // TODO: fix this later??????
      if (this.Quantize)
      {
        calculatedPosition = Math.Round(calculatedPosition * this.QuantumSubdivisions) / this.QuantumSubdivisions;
      }
      
      this.MoverPath.Transform.WorldTranslation = calculatedPosition;
    }
    else
    {
      this.MoverPath.Transform.WorldTranslation = focusPosition;
    }
  }
}

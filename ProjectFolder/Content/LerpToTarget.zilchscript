class LerpToTarget : ZilchComponent
{
  [Property]
  var MoverPath = CogPath(".");  // the object that will move
  [Property]
  var FocusPath = CogPath();  // the object the mover will move to
  [Property][Slider(0, 1, 0.01)]
  var Snappiness : Real = 0.1;
  
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }
  
  
  function OnLogicUpdate(event : UpdateEvent)
  {
    var moverPosition = this.MoverPath.Transform.WorldTranslation;
    var focusPosition = this.FocusPath.Transform.WorldTranslation;
    var difference = focusPosition - moverPosition;
    
    // TODO:
    // 
    // this could maybe be slightly rewritten to take snappiness into account
    if (Math.LengthSq(difference) >= 0.000001)
    {
      var movement = difference * this.Snappiness;
      
      this.MoverPath.Transform.WorldTranslation = moverPosition + movement;
    }
    else
    {
      this.MoverPath.Transform.WorldTranslation = focusPosition;
    }
  }
}

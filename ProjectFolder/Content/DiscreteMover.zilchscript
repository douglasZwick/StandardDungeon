class DiscreteMover : ZilchComponent
{
  sends MovementAttempted : MovementEvent;
  sends MovementStarted : MovementEvent;
  sends MovementFailed : MovementEvent;
  
  
  [Dependency]
  var Transform : Transform;
  
  
  [Property]
  var ColliderPath : CogPath = CogPath(".");
  
  var Collider : Collider;
  var CastFilter : CastFilter = CastFilter();
  var Tracker : CustomCollisionEventTracker;
  
  
  function Initialize(init : CogInitializer)
  {
    this.Collider = this.ColliderPath.Collider;
    this.Tracker = this.Owner.CustomCollisionEventTracker;
    
    this.CastFilter.CollisionGroup = this.Collider.CollisionGroup;
    this.CastFilter.IgnoreGhost = true;
  }
  
  
  function Move(movement : Real3)
  {
    // here's how movement works:
    // 
    // 1. dispatch the MovementAttempted event
    // 2. check to see if the desired movement is possible
    // 3a. if it is:
    //   4a. dispatch the MovementStarted event to this.Owner
    //   5a. immediately move the character to the new position. this ensures that no other
    //       characters can go to the same tile before movement is complete
    // 3b. if movement is not possible:
    //   4b. dispatch the MovementFailed event to this.Owner
    // 
    // that's it, that's all that's needed
    
    var me = MovementEvent();
    me.OldPosition = this.Transform.WorldTranslation;
    me.AttemptedPosition = me.OldPosition + movement;
    this.Owner.DispatchEvent(Events.MovementAttempted, me);
    
    var moveResults = this.Space.PhysicsSpace.CastCollider(movement, this.Collider, this.CastFilter);
    var solid = moveResults.IsNotEmpty;
    
    if (this.Tracker != null)
    {
      foreach (var result in moveResults)
      {
        var other = result.ObjectHit;
        var otherPosition = other.Transform.WorldTranslation;
        var thisPosition = this.Transform.WorldTranslation;
        var difference = otherPosition - thisPosition;
        var normalTowardOther = Math.Normalize(difference);
        
        this.Tracker.AddCollision(result.Collider, other.Transform.WorldTranslation, normalTowardOther);
      }
      
      this.Tracker.SendEvents("Collision");
    }
    
    if (solid)
    {
      me.NewPosition = me.OldPosition;
      this.Owner.DispatchEvent(Events.MovementFailed, me);
    }
    else
    {
      me.NewPosition = me.AttemptedPosition;
      this.Owner.DispatchEvent(Events.MovementStarted, me);
      
      this.Transform.WorldTranslation = me.NewPosition;
    }
  }
}


class MovementEvent : ZilchEvent
{
  // where you started
  var OldPosition : Real3;
  // where you ended up. not relevant during MovementAttempted, so don't use it then
  var NewPosition : Real3;
  // where you would have gone if movement hadn't failed (for MovementFailed)
  var AttemptedPosition : Real3;
}

class SkellyController : ZilchComponent
{
  sends SkellyTick : ZilchEvent;
  
  
  [Dependency]
  var Mover : DiscreteMover;
  
  
  [Property]
  var BaseTickTime : Real = 1.0;
  var TickTimer : Real = 0;
  var PlayerRef : Cog;
  
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    Zero.Connect(this.Owner, Events.SkellyTick, this.OnSkellyTick);
    Zero.Connect(this.Owner, Events.WasDamaged, this.OnWasDamaged);
    Zero.Connect(this.Owner, Events.Death, this.OnDeath);
    
    this.PlayerRef = this.Space.FindObjectByName("Hero");
  }
  
  
  function OnDeath(event : HealthEvent)
  {
    this.Owner.Destroy();
  }
  
  
  function OnWasDamaged(event : HealthEvent)
  {
    Console.WriteLine("I just took `event.Damage` damage!!!");
  }
  
  
  function OnSkellyTick(event : ZilchEvent)
  {
    var hDiff : Real = this.PlayerRef.Transform.Translation.X - this.Owner.Transform.Translation.X;
    var vDiff : Real = this.PlayerRef.Transform.Translation.Y - this.Owner.Transform.Translation.Y;
    
    if(Math.Abs(hDiff) > Math.Abs(vDiff))
    {
      //Move horizontally
      if(hDiff < 0)
      {
        //Move left
        this.Mover.Move(-Real3.XAxis);
      }
      else
      {
        //Move right
        this.Mover.Move(Real3.XAxis);
      }
    }
    else
    {
      //Move vertically
      if(vDiff < 0)
      {
        //Move down
        this.Mover.Move(-Real3.YAxis);
      }
      else
      {
        //Move up
        this.Mover.Move(Real3.YAxis);
      }
    }
  }
  
  
  function OnLogicUpdate(event : UpdateEvent)
  {
    this.TickTimer += event.Dt;
    
    if(this.TickTimer >= this.BaseTickTime)
    {
      this.TickTimer -= this.BaseTickTime;
      this.Owner.DispatchEvent(Events.SkellyTick, ZilchEvent());
    }
  }
}

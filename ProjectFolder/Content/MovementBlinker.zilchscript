class MovementBlinker : ZilchComponent
{
  sends MovementBlinkEnded : ZilchEvent;
  
  
  [Dependency]
  var Sprite : Sprite;
  [Dependency]
  var Transform : Transform;
  
  
  [Property]
  var GhostArchetype : Archetype = Archetype.MovementGhost;
  
  var OutDuration : Real = 6 / 60.0;
  var InDelay     : Real = 3 / 60.0;
  var InDuration  : Real = 6 / 60.0;
  var FadeSequence : ActionSequence;
  var StartingColor : Real4;
  var FadeColor : Real4;
  
  
  function Initialize(init : CogInitializer)
  {
    this.FadeSequence = Action.Sequence(this.Owner.Actions);
    this.StartingColor = this.Sprite.VertexColor;
    this.FadeColor = this.StartingColor;
    this.FadeColor.W = 0;
    
    Zero.Connect(this.Owner, Events.MovementStarted, this.OnMovementStarted);
    Zero.Connect(this.Owner, Events.MovementFailed, this.OnMovementStarted);
  }
  
  
  function OnMovementStarted(event : MovementEvent)
  {
    if (this.Space.MarkedForDestruction)
      return;
    
    var ghost = this.Space.CreateAtPosition(this.GhostArchetype, event.OldPosition);
    ghost.Sprite.SpriteSource = this.Sprite.SpriteSource;
    ghost.Sprite.SetFrame(this.Sprite.CurrentFrame);
    ghost.Sprite.VertexColor = this.Sprite.VertexColor;
    ghost.Sprite.FlipX = this.Sprite.FlipX;
    ghost.Sprite.FlipY = this.Sprite.FlipY;
    ghost.Transform.WorldRotation = this.Transform.WorldRotation;
    
    var ghostSeq = Action.Sequence(ghost.Actions);
      
      Action.Property(ghostSeq, @ghost.Sprite.VertexColor, this.FadeColor, this.OutDuration, Ease.QuadInOut);
      Action.Call(ghostSeq, ghost.Destroy);
    
    this.Sprite.VertexColor = this.FadeColor;
    
    this.FadeSequence.Cancel();
    this.FadeSequence = Action.Sequence(this.Owner.Actions);
      
      Action.Delay(this.FadeSequence, this.InDelay);
      Action.Property(this.FadeSequence, @this.Sprite.VertexColor, this.StartingColor, this.InDuration, Ease.QuadInOut);
      Action.Call(this.FadeSequence, this.EndMovement);
  }
  
  
  function EndMovement()
  {
    this.Owner.DispatchEvent(Events.MovementBlinkEnded, ZilchEvent());
  }
}
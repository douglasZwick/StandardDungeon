class HeroController : ZilchComponent
{
  [Dependency]
  var Mover : DiscreteMover;
  
  
  var Gamepad : Gamepad;
  var Aiming : Boolean;
  
  var CanMove : Boolean
  {
    get
    {
      // TODO: do this better
      var attacking = Zero.Keyboard.KeyIsDown(Keys.Space) ||
                      this.Gamepad.IsButtonHeld(Buttons.A);
      
      return !attacking && !this.Aiming;
    }
  }
  
  
  function Initialize(init : CogInitializer)
  {
    this.Gamepad = Zero.Gamepads.GetGamePad(0);
    
    Zero.Connect(this.Owner, Events.AimingStarted, this.OnAimingStarted);
    Zero.Connect(this.Owner, Events.AimingEnded, this.OnAimingEnded);
    Zero.Connect(Zero.Keyboard, Events.KeyDown, this.OnKeyDown);
    Zero.Connect(this.Gamepad, Events.ButtonDown, this.OnButtonDown);
  }
  
  
  function OnAimingStarted(event : WandEvent)
  {
    this.Aiming = true;
  }
  
  
  function OnAimingEnded(event : WandEvent)
  {
    this.Aiming = false;
  }
  
  
  function OnKeyDown(event : KeyboardEvent)
  {
    if (!this.CanMove) return;
    
    if      (event.Key == Keys.Right || event.Key == Keys.D)
      this.Mover.Move(+Real3.XAxis);
    else if (event.Key == Keys.Left || event.Key == Keys.A)
      this.Mover.Move(-Real3.XAxis);
    else if (event.Key == Keys.Up || event.Key == Keys.W)
      this.Mover.Move(+Real3.YAxis);
    else if (event.Key == Keys.Down || event.Key == Keys.S)
      this.Mover.Move(-Real3.YAxis);
  }
  
  
  function OnButtonDown(event : GamepadEvent)
  {
    if (!this.CanMove) return;
    
    if      (event.Button == Buttons.DpadRight)
      this.Mover.Move(+Real3.XAxis);
    else if (event.Button == Buttons.DpadLeft)
      this.Mover.Move(-Real3.XAxis);
    else if (event.Button == Buttons.DpadUp)
      this.Mover.Move(+Real3.YAxis);
    else if (event.Button == Buttons.DpadDown)
      this.Mover.Move(-Real3.YAxis);
  }
}

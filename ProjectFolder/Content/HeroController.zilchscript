class HeroController : ZilchComponent
{
  [Dependency]
  var Mover : DiscreteMover;
  
  
  var Gamepad : Gamepad;
  
  var OnAWeb : Boolean;
  
  var CanMove : Boolean
  {
    get
    {
      // TODO: do this better
      var attacking = Zero.Keyboard.KeyIsDown(Keys.Space) ||
                      this.Gamepad.IsButtonHeld(Buttons.A);
      
      return !attacking && !this.OnAWeb;
    }
  }
  
  
  function Initialize(init : CogInitializer)
  {
    this.Gamepad = Zero.Gamepads.GetGamePad(0);
    
    Zero.Connect(Zero.Keyboard, Events.KeyDown, this.OnKeyDown);
    Zero.Connect(this.Gamepad, Events.ButtonDown, this.OnButtonDown);
  }
  
  
  function OnKeyDown(event : KeyboardEvent)
  {
    this.DamageWeb();
    if (!this.CanMove) return;
    
    if      (event.Key == Keys.Right)
      this.Mover.Move(+Real3.XAxis);
    else if (event.Key == Keys.Left)
      this.Mover.Move(-Real3.XAxis);
    else if (event.Key == Keys.Up)
      this.Mover.Move(+Real3.YAxis);
    else if (event.Key == Keys.Down)
      this.Mover.Move(-Real3.YAxis);
  }
  
  function DamageWeb()
  {
    var contacts = this.Owner.Collider.Contacts;
    
    this.OnAWeb = false;
    foreach(var contact in contacts)
    {
      var spiderWebController : SpiderWebController = contact.OtherObject.SpiderWebController;
      if(spiderWebController != null)
      {
        this.OnAWeb = true;
        spiderWebController.WebStrength -= 1;
        if(spiderWebController.WebStrength < 0)
        {
          spiderWebController.Owner.Destroy();
        }
      }
    }
    //Console.WriteLine(this.OnAWeb);
  }
  
  function OnButtonDown(event : GamepadEvent)
  {
    if (!this.CanMove) return;
    
    if      (event.Button == Buttons.DpadRight)
      this.Mover.Move(+Real3.XAxis);
    else if (event.Button == Buttons.DpadLeft)
      this.Mover.Move(-Real3.XAxis);
    else if (event.Button == Buttons.DpadUp)
      this.Mover.Move(+Real3.YAxis);
    else if (event.Button == Buttons.DpadDown)
      this.Mover.Move(-Real3.YAxis);
  }
}
